// Main Tests File
nextflow_pipeline {

    name "Full Pipeline NF-Tests for MeaSeq"
    script "main.nf"

    //--- Test 1
    test("No platform specified") {
        tag "fail"

        when {
            params {
                input       = "${projectDir}/assets/ci-samplesheet.csv"
                outdir      = "results"
                reference   = "${projectDir}/test_data/MH356245.1.reference.fasta"
            }
        }
        then {
            // Status
            assert workflow.failed

            // Message
            assert workflow.stderr.contains("* Missing required parameter: --platform")
        }
    }

    //--- Test 2
    test("No Input Specified") {
        tag "fail"

        when {
            params {
                platform    = "illumina"
                outdir      = "results"
                reference   = "${projectDir}/test_data/MH356245.1.reference.fasta"
            }
        }
        then {
            // Status
            assert workflow.failed

            // Message
            assert workflow.stderr.contains("* Missing required parameter: --input")
        }
    }

    //--- Test 3
    test("Missing Samplesheet Data") {
        tag "fail"

        when {
            params {
                input       = "$baseDir/test_data/missing.csv"
                platform    = "illumina"
                outdir      = "results"
                reference   = "${projectDir}/test_data/MH356245.1.reference.fasta"
            }
        }
        then {
            // Status
            assert workflow.failed

            // Message
            assert workflow.stdout.contains("ERROR ~ ERROR: Validation of 'input' file failed!")
        }
    }

    //--- Test 4
    test("Illumina Amplicon Data Completion") {
        tag "success"

        when {
            params {
                input       = "${projectDir}/assets/ci-samplesheet.csv"
                outdir      = "results"
                reference   = "${projectDir}/test_data/MH356245.1.reference.fasta"
                platform    = "illumina"
                primer_bed  = "${projectDir}/test_data/primer.bed"
            }
        }
        then {
            //
            // Status
            //
            assert workflow.success
            assert workflow.trace.failed().size() == 0

            //
            // Channels and Files Exist
            //
            def lines = []
            assert path("$launchDir/results").exists()

            // Final QC File
            assert path("$launchDir/results/overall.qc.csv").exists()
            // Using the first 10 columns only to get to variants
            lines = path("$launchDir/results/overall.qc.csv").text
            assert lines.contains("sample,genotype,matched_dsid,num_input_reads,num_aligned_reads,num_consensus_n,genome_completeness_percent,mean_sequencing_depth,median_sequencing_depth,total_variants,")
            assert lines.contains("sample1,D8,NA,40476,39026,224,98.59,349.06,313.0,269,")
            assert lines.contains("sample2,D8,NA,56368,54181,233,98.53,484.7,483.0,273,")

            // Intermediate generated reference files
            assert path("$launchDir/results/reference/amplicon.bed").exists()

            lines = path("$launchDir/results/reference/amplicon.bed").text
            assert lines.contains("MH356245.1\t25\t8200\tMSV_1\t1\t+")
            assert lines.contains("MH356245.1\t8025\t15865\tMSV_2\t2\t+")

            assert path("$launchDir/results/reference/genome.bed").exists()

            lines = path("$launchDir/results/reference/genome.bed").text
            assert lines.contains("MH356245.1\t0\t15894")

            // Sample Outputs
            //  Fasta regions match
            assert path("$launchDir/results/consensus/MH356245.1.N450.fasta").md5 == "79e89ca9bfb7213c3767b2011f9ffee8"
            assert path("$launchDir/results/consensus/sample1.consensus.fasta").md5 == "d7450179ac96cfda3e004a7bb74ea2fd"
            assert path("$launchDir/results/consensus/sample1.N450.fasta").md5 == "49dad01767d044fc73d87a6818765410"
            assert path("$launchDir/results/consensus/sample2.consensus.fasta").md5 == "fcba2244d3181d683411d99a8ace7ba2"
            assert path("$launchDir/results/consensus/sample2.N450.fasta").md5 == "896c7f643c3fdd5af4f0b4c8195a01df"

            //  Reporting files
            assert path("$launchDir/results/reporting/sample_positional_normalized_depth.csv").exists()
            lines = path("$launchDir/results/reporting/sample_positional_normalized_depth.csv").readLines()
            assert lines.size() == 3

            assert path("$launchDir/results/reporting/sample_positional_normalized_depth_stats.csv").exists()
            lines = path("$launchDir/results/reporting/sample_positional_normalized_depth_stats.csv").readLines()
            assert lines.size() == 15895

            // Final Reports
            assert path("$launchDir/results/Amplicon-Summary-MultiQC-Report_multiqc_report.html").exists()
            assert path("$launchDir/results/MeaSeq_Report.html").exists()

            // IRIDA Next output
            def iridanext_json = path("$launchDir/results/iridanext.output.json").json
            def iridanext_global = iridanext_json.files.global
            def iridanext_samples = iridanext_json.files.samples
            def iridanext_metadata = iridanext_json.metadata.samples

            assert iridanext_global.findAll { it.path == "MeaSeq_Report.html" }.size() == 1
            assert iridanext_global.findAll { it.path == "Amplicon-Summary-MultiQC-Report_multiqc_report.html" }.size() == 1

            assert iridanext_samples.sample1.findAll { it.path == "consensus/sample1.N450.fasta" }.size() == 1
            assert iridanext_samples.sample1.findAll { it.path == "consensus/sample1.consensus.fasta" }.size() == 1
            assert iridanext_samples.sample1.findAll { it.path == "vcf/processed_vcf/sample1.processed.norm.vcf.gz" }.size() == 1

            assert iridanext_metadata.sample1.genotype == "D8"
            assert iridanext_metadata.sample1.matched_dsid == "NA"
            assert iridanext_metadata.sample1.num_input_reads == "40476"
            assert iridanext_metadata.sample1.num_aligned_reads == "39026"
            assert iridanext_metadata.sample1.num_consensus_n == "224"
            assert iridanext_metadata.sample1.genome_completeness_percent == "98.59"
            assert iridanext_metadata.sample1.mean_sequencing_depth == "349.06"
            assert iridanext_metadata.sample1.median_sequencing_depth == "313.0"
            assert iridanext_metadata.sample1.total_variants == "269"
            assert iridanext_metadata.sample1.num_snps == "269"
            assert iridanext_metadata.sample1.num_iupac == "0"
            assert iridanext_metadata.sample1.genome_length == "15894"
            assert iridanext_metadata.sample1.divisible_by_6 == "True"
            assert iridanext_metadata.sample1.qc_status == "PASS"

            // Pipeline info and tracking
            assert path("$launchDir/results/pipeline_info").exists()
            assert path("$launchDir/results/pipeline_info/measeq_software_mqc_versions.yml").exists()
            assert path("$launchDir/results/pipeline_info/bco.json").exists()
        }
    }
}
